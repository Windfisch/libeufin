plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.30-RC'
    id 'idea'
}

if (!JavaVersion.current().isJava11Compatible()){
    throw new GradleException(
        "This build must be run with java 11 " +
        "or later (your version is java ${JavaVersion.current()})")
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

idea {
    module {
        excludeDirs += file("frontend")
    }
}

setVersion("0.0.1-dev.1")

task versionFile() {
    new File("${projectDir}/util/src/main/resources", "version.txt").text = getRootProject().version
}

classes {
    dependsOn versionFile
}

task dist(type: Zip) {
    dependsOn versionFile
    evaluationDependsOn("nexus")
    evaluationDependsOn("sandbox")
    def topDir = "${getRootProject().name}-${getRootProject().version}"
    archiveFileName = "${topDir}.zip"
    subprojects.each {
        if (it.name == "nexus" || it.name == "sandbox") {
            Task t = it.tasks.getByName("installShadowDist")
            dependsOn(t)
        }
    }
    from("nexus/build/install/nexus-shadow") {
        include("**/libeufin-nexus")
        include("**/*.jar")
    }
    from("sandbox/build/install/sandbox-shadow") {
        include("**/libeufin-sandbox")
        include("**/*.jar")
    }
    from ("cli") {
        include("**/libeufin-cli")
    }
    into(topDir)
}
