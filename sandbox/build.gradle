plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'application'
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

sourceCompatibility = "11"
targetCompatibility = "11"
version '1.0-snapshot'

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

task installToPrefix(type: Copy) {
    dependsOn(installShadowDist)
    from("build/install/sandbox-shadow") {
        include("**/libeufin-sandbox")
        include("**/*.jar")
    }
    /**
     * Reads from command line -Pkey=value options,
     * with a default (/tmp) if the key is not found.
     *
     * project.findProperty('prefix') ?: '/tmp'
     */
    into "${project.findProperty('prefix') ?: '/tmp'}"
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

sourceSets {
    main.java.srcDirs = ['src/main/java', 'src/main/kotlin']
}

def ktor_version = "1.5.0"
def exposed_version = "0.25.1"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation "javax.xml.bind:jaxb-api:2.3.0"
    implementation group: 'xerces', name: 'xercesImpl', version: '2.6.2-jaxb-1.0.6'
    implementation "javax.activation:activation:1.1"
    implementation "org.glassfish.jaxb:jaxb-runtime:2.3.1"
    implementation 'org.apache.santuario:xmlsec:2.1.4'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk16', version: '1.45'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.28.0'
    implementation "org.postgresql:postgresql:42.2.18"
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.20'
    implementation("com.github.ajalt:clikt:2.7.0")
    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"

    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"

    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.4.30-RC'
    testImplementation 'org.jetbrains.kotlin:kotlin-test:1.4.30-RC'
    testImplementation group: "junit", name: "junit", version: "4.12"

    implementation project(":util")
}


application {
    mainClassName = "tech.libeufin.sandbox.MainKt"
    applicationName = "libeufin-sandbox"
}


jar {
    manifest {
        attributes "Main-Class": "tech.libeufin.sandbox.MainKt"
    }
}